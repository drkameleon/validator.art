;===============================================
; Validator.art
;
; String validation library
; for Arturo
;
; MIT License
; (c) 2024 Yanis ZafirÃ³pulos
;==========================================================
; Date validation
;
; @file: src/validators/date.art
;==========================================================

define :dateValidator is :validator [

    ;------------------
    ; parameters
    ;------------------

    params: #[
        format: @[:string, "yyyy-MM-dd"]  ; default ISO format
    ]

    ;------------------
    ; built-in data
    ;------------------

    ; These are common formats that can be aliased
    formatAliases: #[
        iso:        "yyyy-MM-dd"
        iso8601:    "yyyy-MM-dd'T'HH:mm:ss'Z'"
        short:      "MM/dd/yy"
        long:       "MMMM dd, yyyy"
    ]

    ;------------------
    ; methods
    ;------------------

    action: method [str, opts][
        fmt: opts\format

        if literal? fmt [
            ; get final format (after possible alias resolution)
            unless key? \formatAliases fmt ->
                panic ~"unrecognized format alias: |fmt|"
            
            fmt: \formatAliases\[fmt]
        ]

        ; try converting to date using the format
        ; and catch any potential errors
        return not? throws? [
            _: to :date .format:fmt str
        ]
    ]

    test: method [][
        #[
            valid: [
                ; default ISO format (yyyy-MM-dd)
                "2024-01-31"
                "1999-12-31"
                "2000-02-29"      ; leap year

                ; with different formats
                ["01/31/24"                 [format: 'short]]
                ["January 31, 2024"         [format: 'long]]
                ["2024-01-31T13:45:30Z"     [format: 'iso8601]]

                ; custom formats
                ["20240131"                 [format: "yyyyMMdd"]]
                ["31-Jan-2024"              [format: "dd-MMM-yyyy"]]
                ["01/31/2024"               [format: "MM/dd/yyyy"]]
                ["2024.01.31"               [format: "yyyy.MM.dd"]]
                ["13:45 31/01/2024"         [format: "HH:mm dd/MM/yyyy"]]
            ]

            invalid: [
                ; wrong format for default ISO
                "31-01-2024"
                "2024/01/31"
                "2024.01.31"

                ; invalid dates
                "2024-13-01"      ; invalid month
                "2024-01-32"      ; invalid day
                "2023-02-29"      ; not a leap year
                ""                ; empty string
                "not a date"      ; garbage input
                
                ; wrong format specified
                ["2024-01-31"               [format: "dd-MM-yyyy"]]
                ["31/01/24"                 [format: 'iso]]
                ["Jan 31, 2024"             [format: 'long]]

                ; invalid dates with formats
                ["13/31/24"                 [format: 'short]]       ; invalid month
                ["February 29, 2023"        [format: 'long]]        ; not a leap year
                ["2024-01-31T25:45:30Z"     [format: 'iso8601]]     ; invalid hour
                ["2024-01-31T13:66:4Z"      [format: 'iso8601]]     ; invalid minute
                ["2024-01-31 13:45:30Z"     [format: 'iso8601]]     ; wrong separator

                ; malformed custom formats
                ["202401311"                [format: "yyyyMMdd"]]           ; extra digit
                ["31-January-2024"          [format: "dd-MMM-yyyy"]]        ; full month name
                ["13:45:31/01/2024"         [format: "HH:mm dd/MM/yyyy"]]   ; extra seconds
            ]
        ]
    ]
]