;===============================================
; Validator.art
;
; String validation library
; for Arturo
;
; MIT License
; (c) 2024 Yanis Zafirópulos
;==========================================================
; @file: src/validator.art
;==========================================================

;---------------------------
; The main type
;---------------------------

define :validator [
    ; data
    params: #[]

    ; methods
    action: method [str, opts][
        panic "Validator should define its own action"
    ]

    test: method [][
        panic "Validator should include its own unit-tests"
    ]
]

;---------------------------
; The main module
;---------------------------

export module [
    ;---------------------------
    ; Private variables
    ;---------------------------

    validators: #[]

    ;---------------------------
    ; Initialization
    ;---------------------------

    init: method [][
        loop list ./"validators" 'validator [
            name: extract.filename validator
            do validator
            \validators\[name]: to to :type ~"|name|Validator" []!
        ]
    ]

    ;---------------------------
    ; Private methods
    ;---------------------------

    fetchParams: method [usedParams :dictionary][
        res: #[]

        unless empty? usedParams ->
            loop usedParams [pname, pspec][
                switch pval: <= attr pname [
                    ensure -> is? pspec\0 pval
                    res\[pname]: pval
                ][
                    res\[pname]: pspec\1
                ]
            ]

        return res
    ]

    ;---------------------------
    ; Public methods
    ;---------------------------

    _validators: method.public [][
        ; meant to be used 
        ; mainly by the unit-tests
        return sort \validators
    ]

    valid?: method.public [str :string :block][
        ;; description: « check if given string is valid
        ;; options: [
        ;;      email: « verify e-mail address
        ;;      ip: « verify IP address
        ;;      url: « verify URL
        ;; ]
        ;; returns: :logical
        ;; example: {
        ;;      valid?.url "https://arturo-lang.io"
        ;;      ; => true
        ;;
        ;;      valid?.email "loremIpsum@
        ;;      ; => false
        ;;
        ;;      valid?.email ["my@email.com", "info@google.com"]
        ;;      ; => true
        ;; }

        inputs: (string? str)? -> @[str][
            ensure -> is? [:string] str
            str
        ]
        
        loop \validators [name, validator][
            if attr name [
                options: \fetchParams validator\params
                loop inputs 'inp [
                    unless do [ validator\action inp options ] ->
                        return false
                ]
            ]
        ]
        return true
    ]
]!
